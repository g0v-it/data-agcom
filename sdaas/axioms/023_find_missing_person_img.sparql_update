#
# Trova tutte le immagini presenti su wikidata associate a persone
#
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

# see https://stackoverflow.com/questions/34393884/how-to-get-image-url-property-from-wikidata-item-by-api
INSERT { ?person foaf:img ?imgThumb }
WHERE { 
    ?person a foaf:Person; owl:sameAs ?item
	SERVICE <https://query.wikidata.org/sparql> {
		?item wdt:P18 ?img.
	}
	
	BIND( STR(?img) AS ?urlString)
    BIND( REPLACE(STRAFTER( ?urlString, "Special:FilePath/"),"%20","_") AS ?filename)
    BIND( MD5(?filename) AS ?hash)
    BIND( SUBSTR(?hash, 1, 1) AS ?firstDir)
    BIND( SUBSTR(?hash, 1, 2) AS ?secondDir)
    BIND( IRI( CONCAT( "https://upload.wikimedia.org/wikipedia/commons/thumb/",?firstDir,"/",?secondDir,"/",?filename,"/255px-", ?filename)) AS ?imgThumb)
    
    FILTER NOT EXISTS { ?person foaf:img ?imgThumb  }
}
