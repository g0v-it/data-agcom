# The broadcast weight index (bwi) is a subjective rank related to an estimated audience of TV programs 
# that is computed starting from the potential audience data provided by AUDITEL according this formula:
# bwi(observation) = COALESCE( avg_audience(observation.context), avg_audience(observation.context.nework))

PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX scv: <http://purl.org/NET/scovo#>
PREFIX agcom: <https://g0v-it.github.io/ontologies/agcom#>
PREFIX auditel: <https://g0v-it.github.io/ontologies/auditel#>
PREFIX resource: <http://agcom.linkeddata.cloud/resource/>
PREFIX local: <urn:local:agicom:>

INSERT { ?observation local:bwi ?bwi} 
WHERE {
	?observation agcom:context ?programma;
		qb:dataSet ?dataset ; 
		agcom:speakingTime ?speakingTime.
		
	?programma skos:related ?emittente.
	OPTIONAL { ?x auditel:context ?programma ;auditel:audience ?audience_programma }
	
	?emittente skos:inScheme resource:emittenti.
	OPTIONAL { ?y auditel:context ?emittente  ;auditel:audience ?audience_emittente }

	BIND( COALESCE(?audience_programma, ?audience_emittente) as ?bwi )
	
	FILTER NOT EXISTS  { ?observation local:bwi [] }
}