#
# Create bubbles history records 
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX interval: <http://reference.data.gov.uk/def/intervals/>
PREFIX time: <http://www.w3.org/2006/time#>
PREFIX qb: <http://purl.org/linked-data/cube#>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX agcom: <https://g0v-it.github.io/ontologies/agcom#>
PREFIX resource: <http://agcom.linkeddata.cloud/resource/>
PREFIX scovo: <http://purl.org/NET/scovo#>
PREFIX local: <urn:local:agicom:>

INSERT {
	?account bgo:isVersionOf ?historyRec .
	?historyRec a bgo:VersionedAmount ; 
	  	dct:source ?source2 ;
		bgo:version ?version ;
		bgo:amount ?amount
	.
} WHERE {
    ?account a bgo:Account; dct:source ?source1 .
    ?source1 a qb:Observation ;
    	qb:dataSet/agcom:refPeriod/time:hasEnd/time:inXSDDateTime ?referenceDate ;
    	agcom:subject ?subject ;
    	agcom:role ?role ;
    	agcom:context ?context .

    ?source2 a qb:Observation;
    	qb:dataSet/agcom:refPeriod/time:hasEnd/time:inXSDDateTime ?endDate ;
    	agcom:subject ?subject ;
    	agcom:role ?role ;
    	agcom:context ?context ;          
        local:nst ?amount . 

	BIND( STRBEFORE(STR(?endDate),"T") AS ?version)         
    FILTER( ?endDate < ?referenceDate )
  	BIND( IRI( CONCAT( STR(?source2),"_bh")) AS ?historyRec )
}

