#
# Compute the totals in BubbleGraph partitions
#
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX bgo: <http://linkeddata.center/lodmap-bgo/v1#>
PREFIX accounts: <https://data.agcom.g0v.it/ldp/accounts#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX agcom: <https://g0v-it.github.io/ontologies/agcom#>
PREFIX resource: <http://agcom.linkeddata.cloud/resource/>


#
# Create partition schema for account:soggetto
#
INSERT {
  accounts:soggetto bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; 
  	bgo:partitionAmount ?partitionAmount.
} 
WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
      ?bubble a bgo:Account ; 
                bgo:partitionLabel ?partitionLabel ;
                dct:source/agcom:subject/skos:prefLabel ?partitionLabel ;
                bgo:amount ?amount .
    } GROUP BY ?partitionLabel
  }

  BIND( IRI(CONCAT( STR(accounts:), "soggetto_", MD5(STR(?partitionLabel)))) AS ?partitionUri ) 
}

;


#
# Create partition schema for account:ruolo
#
INSERT {
  accounts:ruolo bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; 
  	bgo:partitionAmount ?partitionAmount.
} 
WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
      ?bubble a bgo:Account ; 
                bgo:partitionLabel ?partitionLabel ;
                dct:source/agcom:role/skos:prefLabel ?partitionLabel ;
                bgo:amount ?amount .
    } GROUP BY ?partitionLabel
  }

  BIND( IRI(CONCAT( STR(accounts:), "ruolo_", MD5(STR(?partitionLabel)))) AS ?partitionUri ) 
}

;

#
# Create partition schema for account:emittente
#
INSERT {
  accounts:emittente bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; 
  	bgo:partitionAmount ?partitionAmount.
} 
WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
      ?bubble a bgo:Account ; 
                bgo:partitionLabel ?partitionLabel ;
                bgo:amount ?amount .

      resource:emittenti skos:hasTopConcept/skos:prefLabel ?partitionLabel
    } GROUP BY ?partitionLabel
  }

  BIND( IRI(CONCAT( STR(accounts:), "emittente_", MD5(STR(?partitionLabel)))) AS ?partitionUri ) 
}

;


#
# Create partition schema for account:editore
#
INSERT {
  accounts:editore bgo:partition ?partitionUri.
  ?partitionUri bgo:label ?partitionLabel; 
  	bgo:partitionAmount ?partitionAmount.
} 
WHERE {
  {
    SELECT DISTINCT ?partitionLabel ( SUM(?amount) AS ?partitionAmount) WHERE  {
      ?bubble a bgo:Account ; 
                bgo:partitionLabel ?partitionLabel ;
                bgo:amount ?amount .

      resource:editori skos:hasTopConcept/skos:prefLabel ?partitionLabel
    } GROUP BY ?partitionLabel
  }

  BIND( IRI(CONCAT( STR(accounts:), "editore_", MD5(STR(?partitionLabel)))) AS ?partitionUri ) 
}



