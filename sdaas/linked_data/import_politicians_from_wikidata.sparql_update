#
# Link agcom resource to linkeddata ones and import data
#
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX wd: <http://www.wikidata.org/entity/>


# Import frow wikidata all politicians, creating hidden labes to index
INSERT {
  	?politician skos:prefLabel ?label;
  		skos:hiddenLabel ?altLabel1, ?altLabel2, ?altLabel3 ;
  		owl:sameAs ?openPolisUri
} WHERE {
  SERVICE <https://query.wikidata.org/sparql> {
    ?politician wdt:P106 wd:Q82955;# a politician
          wdt:P27 wd:Q38;			# italian
          wdt:P1229 ?openpolisId ;	# registered in openpolis
          rdfs:label ?label.
    FILTER(LANG(?label) = "it")
    FILTER NOT EXISTS { ?politician wdt:P570 []} # is not dead
  }
  BIND( IRI( CONCAT("https://politici.openpolis.it/politico/", ?openpolisId) ) AS ?openPolisUri)
  BIND( UCASE(?label) AS ?altLabel1)
  BIND( UCASE(CONCAT(STRAFTER(?label," "), " "@it, STRBEFORE(?label," "))) AS ?altLabel2)
  BIND( UCASE(CONCAT(STRBEFORE(?label," "), " "@it, STRAFTER(?label," "))) AS ?altLabel3)
}

