#
# Trova tutte le immagini presenti su wilidata
#
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#> 
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
PREFIX qb: <http://purl.org/linked-data/cube#> 
PREFIX owl: <http://www.w3.org/2002/07/owl#> 
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#> 
PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
PREFIX dct: <http://purl.org/dc/terms/> 
PREFIX foaf: <http://xmlns.com/foaf/0.1/> 
PREFIX interval: <http://reference.data.gov.uk/def/intervals/> 
PREFIX sdmx-subject: <http://purl.org/linked-data/sdmx/2009/subject#> 
PREFIX agcom: <https://g0v-it.github.io/ontologies/agcom#> 
PREFIX resource: <http://agcom.linkeddata.cloud/resource/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>

# see https://stackoverflow.com/questions/34393884/how-to-get-image-url-property-from-wikidata-item-by-api
SELECT ?item ?imgThumb WHERE {
    ?concept a skos:Concept; owl:sameAs ?item
	SERVICE <https://query.wikidata.org/sparql> {
		?item wdt:P18 ?img.
	}
	
	BIND( STR(?img) AS ?urlString)
    BIND( REPLACE(STRAFTER( ?urlString, "Special:FilePath/"),"%20","_") AS ?filename)
    BIND( MD5(?filename) AS ?hash)
    BIND( SUBSTR(?hash, 1, 1) AS ?firstDir)
    BIND( SUBSTR(?hash, 1, 2) AS ?secondDir)
    BIND( IRI( CONCAT( "https://upload.wikimedia.org/wikipedia/commons/thumb/",?firstDir,"/",?secondDir,"/",?filename,"/255px-", ?filename)) AS ?imgThumb)
}
